/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2022 The JReleaser authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

boolean qualityCheck = (project.rootProject.findProperty('qualityCheck') ?: false).toBoolean()

config {
    info {
        description = 'jreleaser'
        inceptionYear = '2020'
        tags = ['release']

        links {
            website      = "https://github.com/jreleaser/${project.rootProject.name}"
            issueTracker = "https://github.com/jreleaser/${project.rootProject.name}/issues"
            scm          = "https://github.com/jreleaser/${project.rootProject.name}.git"
        }

        scm {
            url                 = "https://github.com/jreleaser/${project.rootProject.name}"
            connection          = "scm:git:https://github.com/jreleaser/${project.rootProject.name}.git"
            developerConnection = "scm:git:git@github.com:jreleaser/${project.rootProject.name}.git"
        }

        specification {
            enabled = true
        }

        implementation {
            enabled = true
        }
    }

    docs {
        javadoc {
            autoLinks {
                enabled = false
            }
        }
    }

    licensing {
        mappings += [
            gdsl: 'SLASHSTAR_STYLE',
            dsld: 'SLASHSTAR_STYLE'
        ]
    }

    quality {
        checkstyle {
            toolVersion = checkstyleVersion
            aggregate {
                enabled = true
            }
        }
        spotbugs {
            enabled = qualityCheck
            toolVersion = spotbugsVersion
            report = 'html'
            aggregate {
                enabled = qualityCheck
            }
        }
        errorprone {
            enabled = qualityCheck
            excludedPaths = '.*/build/mavenPlugin/.*'
            args = [
                '-Xep:MissingSummary:OFF',
                '-Xep:SameNameButDifferent:OFF',
                '-Xep:JavaLangClash:OFF'
            ]
        }
    }

    coverage {
        jacoco {
            enabled = true
            toolVersion = jacocoVersion
        }
        coveralls {
            enabled = true
            standalone = true
        }
    }
}

allprojects {
    repositories {
        mavenLocal()
        gradlePluginPortal()
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

subprojects {
    config {
        info {
            description = project.project_description
        }
    }

    license {
        exclude('build/**')
        exclude('**/*.tpl')
        exclude('META-INF/**/*.yml')
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter:$junit5Version"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$junit5Version"
        testImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
        testImplementation "org.junit-pioneer:junit-pioneer:$junitPioneerVersion"

        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    }

    test {
        useJUnitPlatform()
    }
}

subprojects { p ->
    config {
        coverage {
            jacoco {
                enabled = hasTestsAt(p.file('src/test'))
            }
        }
    }

    pitest {
        outputFormats = ['XML', 'HTML']
        exportLineCoverage = true
        timestampedReports = false
        junit5PluginVersion = '1.1.0'
        failWhenNoMutations = hasTestsAt(p.file('src/test'))
    }
}

projects {
    subprojects {
        dirs(['api', 'core', 'sdks']) {
            compileJava {
                options.compilerArgs += ["-AprojectPath=${project.group}/${project.name}"]
            }

            dependencies {
                compileOnly project(':jreleaser-nativeimage-annotations')
                annotationProcessor project(':jreleaser-nativeimage-processor')
            }
        }
    }
}

// Delete Gradle artifacts not suitable for publication to Maven Central
tasks.register('cleanupStagedRepository') {
    doLast {
        def repoDir = project.layout.buildDirectory.dir('repos/local/release/org/jreleaser').get()
        repoDir.dir('jdks').asFile.deleteDir()
        repoDir.dir('jdks-gradle-plugin').asFile.deleteDir()
        repoDir.dir('jreleaser-gradle-plugin').asFile.deleteDir()
        repoDir.dir('org.jreleaser.gradle.plugin').asFile.deleteDir()
    }
}

boolean hasTestsAt(File testDir) {
    testDir.exists() && testDir.listFiles()?.size()
}