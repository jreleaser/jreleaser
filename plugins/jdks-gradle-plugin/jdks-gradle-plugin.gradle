/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2025 The JReleaser authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'groovy'
    id 'org.kordamp.gradle.groovy-project'
    id 'org.kordamp.gradle.plugin'
}

config {
    plugins {
        plugin {
            name = project.pluginShortName
            id = project.pluginId
            implementationClass = project.pluginImplementationClass
        }
    }
}

dependencies {
    compileOnly "dev.gradleplugins:gradle-api:$gradleVersion"

    api project(':jreleaser-utils')
    api "de.undercouch:gradle-download-task:$downloadPluginVersion"
}

processResources {
    inputs.property('version', project.version)
    filesMatching(['**/*.properties']) {
        expand(
            'version': project.version,
            'id': 'jdks',
            'name': project.pluginDisplayName
        )
    }
}

afterEvaluate {
    def publishPluginJar = project.tasks.findByName('publishPluginJar')
    def publishTask = project.tasks.findByName('publishMainPublicationToMavenLocal')
    if (publishTask && publishPluginJar) publishTask.dependsOn(publishPluginJar)
    publishTask = tasks.findByName('publishMainPublicationToLocalSnapshotRepository')
    if (publishTask && publishPluginJar) publishTask.dependsOn(publishPluginJar)
    publishTask = tasks.findByName('publishMainPublicationToLocalReleaseRepository')
    if (publishTask && publishPluginJar) publishTask.dependsOn(publishPluginJar)
}
