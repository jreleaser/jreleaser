/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2025 The JReleaser authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'com.github.johnrengelman.shadow'
    id 'application'
    id 'org.moditect.gradleplugin'
}

application {
    mainClass = 'org.jreleaser.tool.JReleaser'
    // shadow complains if this is not set
    mainClassName = 'org.jreleaser.tool.JReleaser'
}

config {
    info {
        bytecodeVersion = project.findProperty('targetCompatibility')
    }
    docs {
        javadoc {
            empty = true
        }
    }
}

configurations {
    uberjar
}

dependencies {
    uberjar project(':jreleaser')
    uberjar project(':jreleaser-graalvm-sdk')

    compileOnly "org.kordamp.jipsy:jipsy-annotations:${jipsyVersion}"
    annotationProcessor "org.kordamp.jipsy:jipsy-processor:${jipsyVersion}"
}

sourceSets {
    main {
        compileClasspath += configurations.uberjar
        runtimeClasspath += compileClasspath
    }
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.uberjar]
    }
}

addMainModuleInfo {
    version = project.version
    jvmVersion = targetCompatibility
    overwriteExistingFiles = true
    jdepsExtraArgs = ['-q']
    module {
        mainClass = application.mainClass.get()
        moduleInfoFile = file('src/java11/module-info.java')
    }
}

shadowJar {
    configurations    = [project.configurations.uberjar]
    archiveBaseName   = project.name
    archiveClassifier = ''
    mergeServiceFiles()
    exclude 'module-info.class'
    exclude '**/module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/plexus/**'
    exclude 'META-INF/m2e/**'
    exclude 'plugin.xml'
}

tasks.shadowJar.dependsOn(tasks.register('relocateShadowJar', org.jreleaser.gradle.shadow.ConfigureShadowRelocation) { t->
    t.target = tasks.shadowJar
    t.exclude('zstd-jni')
    t.exclude('jreleaser-logger-api')
    t.exclude('jreleaser-model-api')
    t.exclude('jreleaser-resource-bundle')
    t.exclude('jreleaser-utils')
    t.exclude('jreleaser-templates')
})

jar.finalizedBy shadowJar
addMainModuleInfo.mustRunAfter(shadowJar)

def reproducibleTimestamp = tasks.register('reproducibleTimestamp', org.jreleaser.gradle.archive.ReproducibleTimestamp) {t ->
    t.target = tasks.shadowJar
    t.dependsOn(tasks.addMainModuleInfo)
    t.mustRunAfter(tasks.addMainModuleInfo)
    // FIXME: Disable this task on Windows
    t.onlyIf { !(System.getProperty('os.name').toLowerCase(Locale.ENGLISH).contains('windows')) }
}
shadowJar.finalizedBy(reproducibleTimestamp)
assemble.dependsOn(reproducibleTimestamp)

tasks.withType(JavaCompile) { JavaCompile c ->
    c.sourceCompatibility = JavaVersion.VERSION_11
    c.targetCompatibility = JavaVersion.VERSION_11
}

classes.dependsOn(tasks.register('version') {t ->
    t.inputs.property('version', project.version)
    t.outputs.file(project.layout.buildDirectory.file('VERSION'))
    t.doLast {
        project.layout.buildDirectory.file('VERSION').get().asFile.text = project.version
    }
})

['startScripts', 'distTar', 'distZip',
 'startShadowScripts', 'shadowDistTar', 'shadowDistZip'].each { n ->
    tasks.named(n) { t -> t.enabled = false }
}

afterEvaluate {
    def shadowJar = project.tasks.findByName('shadowJar')
    def publishTask = project.tasks.findByName('publishMainPublicationToMavenLocal')
    if (publishTask && shadowJar) publishTask.dependsOn(shadowJar)
    publishTask = tasks.findByName('publishMainPublicationToLocalSnapshotRepository')
    if (publishTask && shadowJar) publishTask.dependsOn(shadowJar)
    publishTask = tasks.findByName('publishMainPublicationToLocalReleaseRepository')
    if (publishTask && shadowJar) publishTask.dependsOn(shadowJar)
}