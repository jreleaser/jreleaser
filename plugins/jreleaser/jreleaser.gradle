/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2025 The JReleaser authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'application'
    id 'org.jreleaser.jdks'
}

application {
    mainClass = 'org.jreleaser.cli.Main'
}

jar {
    manifest {
        attributes([
            'Main-Class': 'org.jreleaser.cli.Main',
            'Automatic-Module-Name': project.findProperty('automatic.module.name')
        ])
    }
}

startScripts {
    classpath = files('$APP_HOME/lib/*')
}

config {
    info {
        specification { enabled = true }
    }
}

configurations {
    graalvm
}

dependencies {
    api project(':jreleaser-engine')
    graalvm project(':jreleaser-graalvm-sdk')
    //config
    api project(':jreleaser-config-yaml')
    api project(':jreleaser-config-toml')
    api project(':jreleaser-config-json')

    annotationProcessor "info.picocli:picocli-codegen:$picocliVersion"
    api "info.picocli:picocli:$picocliVersion"
    runtimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
}

processResources {
    inputs.property('version', project.version)
    filesMatching(['**/Banner*.properties']) {
        expand(
            'version': project.version,
            'id': 'jreleaser',
            'name': 'jreleaser'
        )
    }
}

distributions {
    main {
        contents {
            from('../..') {
                include 'licenses/*'
                include 'LICENSE'
                include 'NOTICE'
                include 'VERSION'
            }
        }
    }
}

jdks {
    zulu25Linux {
        platform = 'linux-x86_64'
        url = 'https://cdn.azul.com/zulu/bin/zulu25.30.17-ca-jdk25.0.1-linux_x64.tar.gz'
        checksum = '471b3e62bdffaed27e37005d842d8639f10d244ccce1c7cdebf7abce06c8313e'
    }
    zulu25LinuxArm {
        platform = 'linux-aarch64'
        url = 'https://cdn.azul.com/zulu/bin/zulu25.30.17-ca-jdk25.0.1-linux_aarch64.tar.gz'
        checksum = '8c5321f16d9f1d8149f83e4e9ff8ca5d9e94320b92d205e6db42a604de3d1140'
    }
    zulu25LinuxMusl {
        platform = 'linux_musl-x86_64'
        url = 'https://cdn.azul.com/zulu/bin/zulu25.30.17-ca-jdk25.0.1-linux_musl_x64.tar.gz'
        checksum = '8a698183bdcf8e1728408eccb8573b9136325684561bf81377e06207709a3dfd'
    }
    zulu25LinuxMuslArm {
        platform = 'linux_musl-aarch64'
        url = 'https://cdn.azul.com/zulu/bin/zulu25.30.17-ca-jdk25.0.1-linux_musl_aarch64.tar.gz'
        checksum = 'f8969fc8eb4b26b40655db4699e51568f945a5d4d3786fd9684e82b6bcc9af05'
    }
    zulu25Windows {
        platform = 'windows-x86_64'
        url = 'https://cdn.azul.com/zulu/bin/zulu25.30.17-ca-jdk25.0.1-win_x64.zip'
        checksum = '72844ba8dddf9259ab9cfda9d515d0c850179705f74278a75973d73f0c5b2d2b'
    }
    zulu25WindowsArm {
        platform = 'windows-aarch64'
        url = 'https://cdn.azul.com/zulu/bin/zulu25.30.17-ca-jdk25.0.1-win_aarch64.zip'
        checksum = '4883cf39e8d83679c8a051ace4dd72759d97195a72aaa6727a83bd4bcb97b022'
    }
    zulu25Osx {
        platform = 'osx-x86_64'
        url = 'https://cdn.azul.com/zulu/bin/zulu25.30.17-ca-jdk25.0.1-macosx_x64.tar.gz'
        checksum = '0154482b317aa63d5158a358e2fab7f0fd6c3c0ba2000b05655c3bcbdd202584'
    }
    zulu25OsxArm {
        platform = 'osx-aarch64'
        url = 'https://cdn.azul.com/zulu/bin/zulu25.30.17-ca-jdk25.0.1-macosx_aarch64.tar.gz'
        checksum = '126061d6046b0c0df0472b361ca7895951d34fef1dd522f222f2c7d8738a39d8'
    }
    graal25Osx {
        platform = 'osx-x86_64'
        url = 'https://download.oracle.com/graalvm/25/archive/graalvm-jdk-25.0.1_macos-x64_bin.tar.gz'
        checksum = 'a762ca1d9a163e32790b9286f3af4c16369729ff27999d8dbab60d7be16cff2f'
    }
    graal25OsxArm {
        platform = 'osx-aarch64'
        url = 'https://download.oracle.com/graalvm/25/archive/graalvm-jdk-25.0.1_macos-aarch64_bin.tar.gz'
        checksum = '19dfd49ad112e6e6c2bdedc507c6859bf21294fa4c164f1be670d469c65b7993'
    }
    graal25Linux {
        platform = 'linux-x86_64'
        url = 'https://download.oracle.com/graalvm/25/archive/graalvm-jdk-25.0.1_linux-x64_bin.tar.gz'
        checksum = 'd4ab02ba1029e639f03374fdf91c242e1d0d49079880e1af1932ea7b7c431837'
    }
    graal25LinuxArm {
        platform = 'linux-aarch64'
        url = 'https://download.oracle.com/graalvm/25/archive/graalvm-jdk-25.0.1_linux-aarch64_bin.tar.gz'
        checksum = 'edd77565c76570a6df5f38e53d54524102f2c1b1cf74eebd9f51e7fc19f667cd'
    }
    graal25Windows {
        platform = 'windows-x86_64'
        url = 'https://download.oracle.com/graalvm/25/archive/graalvm-jdk-25.0.1_windows-x64_bin.zip'
        checksum = '5398592ecdf8f47f4b481896e1e63cbfff4cb8e0ba61538526e3b3795b1a85e5'
    }
}

copyDependencies {
    dependsOn classes
    inputs.files(configurations.runtimeClasspath)
    configuration = 'runtimeClasspath'
}

assemble.dependsOn(copyDependencies, tasks.register('copyGraalvmJavaSdk', Copy) { t->
    t.from configurations.graalvm
    t.into 'build/dependencies/graalvm'
})

afterEvaluate {
    def copyJdksToCache = project.tasks.findByName('copyJdksToCache')
    ['zulu25Linux', 'zulu25LinuxArm', 'zulu25LinuxMusl', 'zulu25LinuxMuslArm',
     'zulu25Windows', 'zulu25WindowsArm', 'zulu25Osx', 'zulu25OsxArm',
     'graal25Osx', 'graal25OsxArm', 'graal25Linux', 'graal25LinuxArm', 'graal25Windows'].each { jdk ->
        def copyTask = project.tasks.findByName('copyJdkFromCache' + jdk.capitalize())
        if (copyJdksToCache && copyTask) copyTask.dependsOn(copyJdksToCache)
    }
}